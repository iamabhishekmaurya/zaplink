plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'io.zaplink'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2025.0.0")
}

dependencies {
	//WEB
	implementation 'org.springframework.boot:spring-boot-starter-web'
	//VALIDATION
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	//DATABASE
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.postgresql:postgresql'
	//MONITORING
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	//QR CODE
	implementation 'com.google.zxing:core:3.4.1'
	implementation 'com.google.zxing:javase:3.4.1'
	//Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	//TESTING
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.8"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				"**/dto/**",
				"**/entity/**",
				"**/common/**"
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.80
			}
		}
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				"**/dto/**",
				"**/entity/**",
				"**/common/**"
			])
		}))
	}
}

test {
	useJUnitPlatform()
}

check {
	dependsOn jacocoTestCoverageVerification
}

task coverageReport(type: JacocoReport) {
	dependsOn test
	group = 'verification'
	description = 'Generates JaCoCo coverage report'
	
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

task checkCoverage(type: JacocoCoverageVerification) {
	dependsOn test
	group = 'verification'
	description = 'Verifies JaCoCo coverage thresholds'
}